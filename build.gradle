plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "io.freefair.lombok" version "6.5.0.2"
}

group = 'dev.badbird'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'lunarclient-public'
        url = 'https://maven.moonsworth.com/repository/lunarclient-public/'
    }
    maven {
        name = 'playpro-repo'
        url = 'https://maven.playpro.com'
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'

    implementation 'net.badbird5907:bLib-Bukkit:2.1.8-REL'

    implementation 'net.octopvp:Commander-Bukkit:1.0-SNAPSHOT'

    implementation 'org.mongodb:mongodb-driver-sync:4.2.2'

    compileOnly 'net.badbird5907:AntiCombatLog:2.1.1'

    compileOnly files('./run/plugins/bukkitapi.jar')

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    compileOnly 'net.coreprotect:coreprotect:21.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
/*
jar {
    archiveFileName = 'TeamsPlus-' + version + '.jar'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE;

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
 */
task copyPlugin(type: Copy) {
    from 'build/libs/TeamsPlus.jar'
    into 'run/plugins'
}
copyPlugin.dependsOn(shadowJar)
task runDev(type: JavaExec) {
    standardInput = System.in
    classpath = files('run/paper.jar')
    //run the serverJar, main from MANIFEST.MF
    //args('nogui')
    workingDir(file('run'))
    args = ['nogui']
}
runDev.dependsOn(project.tasks.copyPlugin)
compileJava {
    options.compilerArgs << '-parameters'
}
shadowJar {
    archiveBaseName.set('TeamsPlus')
    archiveClassifier.set('')
    archiveVersion.set('')

    dependencies {
        include dependency('net.badbird5907:bLib-Bukkit:2.1.8-REL')
        include dependency('net.octopvp:Commander-Bukkit:1.0-SNAPSHOT')
        include dependency('org.mongodb:mongodb-driver-sync:4.2.2')
    }

    relocate 'net.badbird5907.blib', 'net.badbird5907.teams.relocate.bLib'
    relocate 'net.octopvp.commander', 'net.badbird5907.teams.relocate.commander'
    relocate 'com.mongodb', 'net.badbird5907.teams.relocate.mongodb'

    exclude '*.txt'
    exclude '*.md'
    exclude '*.md'
    exclude 'LICENSE'
    exclude 'AUTHORS'
}
